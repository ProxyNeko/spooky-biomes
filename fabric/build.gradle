plugins {
    id "multiloader-loader"
    id "fabric-loom"
}
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft}:${project.parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "maven.modrinth:jade:${jade_fabric_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${project.terrablender_fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
    modImplementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-fabric-${project.minecraft_version}:${project.resourceful_config_version}"
}

loom {
    def aw = project(":common").file("src/main/resources/META-INF/${project.mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${project.mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")

            if (project.hasProperty("mc_uuid")) {
                programArgs("--uuid", project.mc_uuid)
            }

            if (project.hasProperty("mc_username")) {
                programArgs("--username", project.mc_username)
            }
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}
